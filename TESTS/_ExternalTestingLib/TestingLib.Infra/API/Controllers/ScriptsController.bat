@ECHO OFF


SET _$$$CURRENT_NAMESPACE$$$_=TestingLib.Infra.API.Controllers
SET _$$$_CURRENT_SCRIPT_PATH_$$$_=%~dp0

SET TESTING_LIB_INFRA_PATH=%_$$$_CURRENT_SCRIPT_PATH_$$$_%../..
SET TESTING_LIB_INFRA_COMMON_PATH=%TESTING_LIB_INFRA_PATH%/_Common

REM Recup. de TESTING_LIB_APPLICATION_USE_CASES_PATH et TESTING_LIB_INFRA_API_ENUMS_PATH, TESTING_LIB_DOMAIN_SCRIPTS_ENUMS_PATH
CALL "%TESTING_LIB_INFRA_COMMON_PATH%/_Pathes.bat"


Rem Recup. de TESTING_LIB_INFRA_API_ENUMS__ASSERTION_RESULT_DISPLAYER_SCRIPT_ID, etc...
CALL "%TESTING_LIB_INFRA_API_ENUMS_PATH%/Scripts.bat"

Rem Recup. de TESTING_LIB_ASSERTION_RESULT_DISPLAYER_SCRIPT_ID, etc...
CALL "%TESTING_LIB_DOMAIN_SCRIPTS_ENUMS_PATH%/Scripts.bat"






REM ==================== FUNCTION CALL ====================================	
IF %1. EQU Get. (
  Rem --- Conversion de Enums exposés vers Enums internes au Domaine --- 
  Rem %3 : Id (Enum pour l'exterieur), de l'URL du Script à retourner
  CALL :LoadInputEnumsConversionTable
  SET __TESTING_LIB_DOMAIN_SCRIPT_ID__=!ENUM_CONVERSION_%~3!
  CALL "%TESTING_LIB_COMMON_CHECK_FATAL_ERRORS_SCRIPT%" CheckVarExists "__TESTING_LIB_DOMAIN_SCRIPT_ID__"

  Rem --- Appel du UseCase ---
  SET __SCRIPT_GET_URL_USE_CASE_RESPONSE__=
  CALL "%TESTING_LIB_APPLICATION_USE_CASES_PATH%/ScriptGetUrlUseCase.bat" "!__TESTING_LIB_DOMAIN_SCRIPT_ID__!" __SCRIPT_GET_URL_USE_CASE_RESPONSE__

  Rem Renvoi de l'URL du Script, par référence
  SET %2=!__SCRIPT_GET_URL_USE_CASE_RESPONSE__!
	
) 

GOTO :EOF


REM ============================================== LOCAL FUNCTIONS ======================================================

:LoadInputEnumsConversionTable
  SET "ENUM_CONVERSION_%TESTING_LIB_INFRA_API_ENUMS__ASSERTION_RESULT_DISPLAYER_SCRIPT_ID%=%TESTING_LIB_ASSERTION_RESULT_DISPLAYER_SCRIPT_ID%"
GOTO :EOF